<DOCTYPE html>
<html lang="en">
    <head>
        <title>Part B - SDF Scene</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				background:#777;
				padding:0;
				margin:0;
				overflow:hidden;
			}
			#info {
				position: absolute;
				top: 0px;
				width: 100%;
				color: #ffffff;
			}
		</style>
        <script id="vertexShader" type="x-shader/x-vertex">
        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        attribute vec3 position;
        attribute vec2 uv;
        attribute vec3 normal;

        void main() {
            gl_Position = projectionMatrix  * viewMatrix * modelMatrix  * vec4( position, 1.0 );
        }
        </script>
        <script id="fragmentShader" type="x-shader/x-fragment">
        precision mediump float;
        uniform vec2 u_resolution;
        uniform vec3 light_pos;
        uniform vec3 light_diff;
        uniform vec3 light_spec;
        uniform float rote;

        mat3 rotateX(float theta){
            float c =cos(theta);
            float s = sin(theta);

            return mat3(
                vec3(1, 0, 0),
                vec3(0, c, -s),
                vec3(0, s, c)
            );
        }

        float smin(float a, float b){
            float k = 32.0;
            float res = exp(-k*a) + exp(-k*b);
            return -log(res)/k;
        }

        float torusSDF(vec3 p, vec3 tamount){
            p = vec3(rotateX(rote/180.0) * p);
            p = vec3(p.xyz + tamount.xyz);
            vec2 q = vec2(length(p.xz)-0.65, p.y);
            return length(q)-0.5;
        }

        float boxSDF(vec3 p){
            p = vec3(rotateX(rote/180.0) * p);
            p.x = p.x -2.0;
            vec3 d = abs(p) - vec3(0.5, 0.8, 0.5);
            return min(max(d.x, max(d.y, d.z)), 0.0)+length(max(d, 0.0));
        }

        float cylinderSDF(vec3 p){
            p = vec3(rotateX(rote/180.0) * p);
            vec3 c = vec3(0.2, 1.0, 0.0);
            return max(length(p.xz)-c.x, abs(p.y) - c.y);
        }

        float boxtorusBlend(vec3 p){
            float d1 = boxSDF(p);
            float d2 = torusSDF(p, vec3(-2.0,1.0, 1.0));
            return smin(d1, d2);
        }

        float sceneSDF(vec3 p){
            float a = torusSDF(p,vec3(2.0, 0.0, 0.0));
            float b = cylinderSDF(p);
            float c = boxtorusBlend(p);

            float min1 = min(a,b);
            float min2 = min(b,c);
            return min(min1, min2);
        }
        float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end){
            float depth = start;
            for(int i = 0; i < 255; i++){
                float dist = sceneSDF(eye.xyz+(depth*marchingDirection.xyz));
                if(dist < .00001){
                    return depth;
                }
                depth += dist;
                if(depth >= end){
                    return end;
                }
            }
            return end;
        }

        vec3 rayDirection(float fov, vec2 size, vec2 fragCoord){
            vec2 xy = fragCoord.xy - size.xy/2.0;
            float z = size.y / tan(radians(fov)/2.0);
            return normalize(vec3(xy, -z));
        }

        vec3 estimateNormal(vec3 p){
            return normalize(vec3(sceneSDF(vec3(p.x + 0.0001, p.y, p.z)) -
             sceneSDF(vec3(p.x - 0.0001, p.y, p.z)), sceneSDF(vec3(p.x, p.y+ 0.0001, p.z)) -
             sceneSDF(vec3(p.x, p.y - 0.0001, p.z)),sceneSDF(vec3(p.x, p.y, p.z + 0.0001)) -
             sceneSDF(vec3(p.x, p.y, p.z - 0.0001))
            ));
        }

        vec3 phongShading(vec3 p, vec3 eye){
            vec3 norm = estimateNormal(p.xyz);
            vec3 light = normalize(light_pos.xyz - p.xyz);
            vec3 view = normalize(eye.xyz - p.xyz);
            vec3 reflection = normalize(reflect(-light, norm));

            float ndotl = max(0.0, dot(norm, light));
            float rdotv = max(0.0, dot(reflection, view));
            // not in light

            // reflection not visible to viewer
            if(rdotv < 0.0) return vec3(light_diff.xyz * ndotl);
            // note the arbitrary glossiness constant
            vec3 color = light_diff * ndotl + light_spec * pow(rdotv, 0.98);
            return color;
        }

        void main(){
            vec3 dir = rayDirection(45.0, u_resolution.xy, gl_FragCoord.xy);
            vec3 eye = vec3(0.0,0.0,15.0);
            float dist = shortestDistanceToSurface(eye, dir, 0.0, 1000.0);
            if(dist > 1000.0 - 0.001){
                gl_FragColor = vec4(0.0,0.0,0.0,0.0);
            } else {
                vec3 kd = vec3(1.0, 0.0, 0.0);
                vec3 ka = vec3(0.1, 0.0, 0.0);
                vec3 p = eye.xyz + dist * dir.xyz;
                vec3 phong = vec3(phongShading(p.xyz, eye.xyz));
                vec3 color = clamp(kd.xyz * phong.xyz, 0.0, 1.0);
                gl_FragColor =vec4(color.xyz + ka.xyz, 1.0);
            }
        }
        </script>
        <script src="../js/three.js"></script>
        <script src="../js/dat.gui.min.js"></script>
        <script src="../js/OrbitControls.js"></script>
        <script src="../js/GPUParticleSystem.js"></script>
    </head>
    <body>
        <div id="container"></div>
        <script src="Driver.js"></script>
        <script>main()</script>
    </body>
<html>
